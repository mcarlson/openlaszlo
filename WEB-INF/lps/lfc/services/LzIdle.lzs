/**
 *
 * @copyright Copyright 2001-2009 Laszlo Systems, Inc.  All Rights Reserved.
 *            Use is subject to license terms.
 *
 * @affects lzidle
 * @access public
 * @topic LFC
 * @subtopic Services
 */

/**
 * The idle event is a special event that enables/disables the idle
 * kernel callback, depending on whether the event is ready or not
 *
 * @access private
 */
class LzIdleEvent extends LzEvent {
  var registered:Boolean = false;

  function LzIdleEvent(eventSender:LzEventable, eventName:String, d:* = null) {
    super(eventSender, eventName, d);
    if (this.ready && (! this.registered)) {
      this.registered = true;
      LzIdleKernel.addCallback( this, 'sendEvent' );
    }
  }

  public override function addDelegate (d:LzDelegate) :void {
    super.addDelegate(d);
    if (this.ready && (! this.registered)) {
      this.registered = true;
      LzIdleKernel.addCallback( this, 'sendEvent' );
    }
  }

  public override function removeDelegate (d:LzDelegate = null) :void {
    super.removeDelegate(d);
    if ((! this.ready) && this.registered) {
      LzIdleKernel.removeCallback( this, 'sendEvent' );
      this.registered = false;
    }
  }

  public override function clearDelegates () :void {
    super.clearDelegates();
    if ((! this.ready) && this.registered) {
      LzIdleKernel.removeCallback( this, 'sendEvent' );
      this.registered = false;
    }
  }
}


/**
 * <p><code>lz.Idle</code> is the single instance of the class
 * <code>lz.IdleService</code>.</p>
 *
 * <p>The OpenLaszlo runtime generates idle events at a fixed,
 * unspecified frequency.  Here is a simple example that shows how to
 * receive the <event>onidle</event> event:</p>
 *
 * <example><programlisting><![CDATA[
 * <canvas height="80">
 *   <text name="display" />
 *   <attribute name="counter" value="0" />
 *   <handler name="onidle" reference="LzIdleService.LzIdle">
 *     display.format("Received %d idle events", ++counter);
 *   </handler>
 * </canvas>
 * ]]></programlisting></example>
 *
 * @shortdesc Idle service.
 *
 * @devnote This object sends an 'onidle' event when there is no synchronous script
 * running after each frame update.
 */
public final class LzIdleService extends LzEventable {
    /** @access private */
    var coi :Array;
    /** @access private */
    var regNext :Boolean = false;
    /** @access private */
    var removeCOI :LzDelegate = null;

    /** This is the idle event for the system, sent by this service
     * @lzxtype event
     */
    var onidle :LzEvent;

    /**
     * The idle service.  Also available as the global
     * <code>lz.Idle</code>.
     *
     * @type LzIdleService
     * @keywords readonly
     * @devnote this should be a public getter to enforce readonly
     */
    public static const LzIdle:LzIdleService;

    /** @access private
     * @devnote AS3 does not allow private constructors, so we need the
     * error
     */
    function LzIdleService () {
        super();
        //    if (LzIdleService.LzIdle) {
        //      throw new Error("There can be only one LzIdle");
        //    }
        // Create array on instance, not prototype
        this.coi = new Array;
        this.removeCOI = new LzDelegate( this, "removeCallIdleDelegates" );
        this.onidle = new LzIdleEvent(this, 'onidle');
    }

    LzIdleService.LzIdle = new LzIdleService();

    /**
     * Calls the given delegate at the next idle event. This can be used
     * for a non-repeating callback.
     *
     * @param LzDelegate d: The delegate to be called on the next idle
     * event.
     */
    function callOnIdle (d:LzDelegate) :void {
        this.coi.push(d);
        if (! this.regNext) {
            this.regNext = true;
            this.removeCOI.register( this , "onidle" );
        }
    }

    /**
     * @access private
     */
    function removeCallIdleDelegates (t:*) :void {
        var arr:Array = this.coi;
        this.coi = new Array;
        //call in order
        for (var i:int = 0; i < arr.length; i++) {
            arr[i].execute( t );
        }

        if (this.coi.length == 0) {
            // Note that the executed items may have added to the (new)
            // coi queue, only unregister if the queue is empty
            this.removeCOI.unregisterFrom(this.onidle);
            this.regNext = false;
        }
    }

    /**
     * Sets the frame rate (FPS) used for the idle update.
     * @param Number fps: The number of frames per second to use for idle events
     */
    function setFrameRate(fps:Number = 30) :void {
        LzIdleKernel.setFrameRate(fps);
    }

}
lz.IdleService = LzIdleService;  // publish

/**
 * @shortdesc Alias for <link linkend="LzIdleService">LzIdleService.LzIdle</link>
 */
lz.Idle = LzIdleService.LzIdle;
